# add a target to generate API documentation with Doxygen
find_package(Doxygen)
option(BUILD_DOCUMENTATION "Create and install the HTML based API documentation (requires Doxygen)" ${DOXYGEN_FOUND})

if(BUILD_DOCUMENTATION)
	if(NOT DOXYGEN_FOUND)
		message(FATAL_ERROR "Doxygen is needed to build the documentation.")
	endif()

	set(doxyfile_in ${CMAKE_CURRENT_SOURCE_DIR}/Doxyfile.in)
	set(doxyfile ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile)

	configure_file(${doxyfile_in} ${doxyfile} @ONLY)

	add_custom_target(doc
		ALL
		COMMAND ${DOXYGEN_EXECUTABLE} ${doxyfile}
		WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
		COMMENT "Generating API documentation with Doxygen"
		VERBATIM)

	install(DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/doxygen/html TYPE DOC)
endif()

install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/../popt.3
	DESTINATION ${CMAKE_INSTALL_MANDIR}/man3)

install(FILES
	${CMAKE_CURRENT_SOURCE_DIR}/../COPYING
	${CMAKE_CURRENT_SOURCE_DIR}/../CREDITS
	${CMAKE_CURRENT_SOURCE_DIR}/../README
	TYPE DOC)
